
// This script is used to load the mapset names from the language file and store them in an array for later use.

#define MAX_MAPSETS 1024
#define MAX_ACTORS 100
#define MAX_WEAPONS 16
#define MAX_OBITUARIES 32
#define MAX_TAGS 32
#define MAX_MESSAGES 32

struct mapSet
{
    str title;                      // Mapset title (xxxx_MAPSET_TITLE)
    str acronym;                    // Mapset acronym (xxxx_MAPSET_ACRONYM)
    str authors;                    // Mapset authors (xxxx_MAPSET_AUTHORS)
    str description;                // Mapset description (xxxx_MAPSET_DESCRIPTION)
    str custom;                     // Mapset has custom assets(xxxx_MAPSET_CUSTOM)
    str maps;                       // Mapset map count (xxxx_MAPSET_MAPS)
    str date;                       // Mapset date (xxxx_MAPSET_DATE)
    str thumbnail;                  // Mapset thumbnail (xxxxTN)
    str actors[MAX_ACTORS];         // Mapset actors (xxxx_MAPSET_ACTOR_xxx)
    str alt_actors[MAX_ACTORS];     // Mapset alt actors (xxxx_MAPSET_ACTORALT_xxx)
    str weapons[MAX_WEAPONS];       // Mapset weapons (xxxx_MAPSET_WEAPON_xxx)
    str obituaries[MAX_ACTORS];     // Mapset obituaries (xxxx_MAPSET_OB_xxx)
    str tags[MAX_TAGS];             // Mapset tags (xxxx_MAPSET_TAG_xxx)
    str messages[MAX_MESSAGES];     // Mapset messages (xxxx_MAPSET_MSG_xxx)
};

struct mapSet mapSets[MAX_MAPSETS];
int mapSetCount = 0;


script "LexiconExpansionsLoad" open 
{
    if (StrICmp(strparam(n:PRINTNAME_LEVEL),"VR") != 0) { terminate; }
    int i = 1;
    while(i < MAX_MAPSETS)
    {
        str acronym = getDynLangEntry("ADD", "MAPSET", strparam(i:i));
        if(acronym != "_LANG_UNDEFINED_")
        {
            mapSets[mapSetCount].title = getDynLangEntry(acronym, "MAPSET", "TITLE");
            mapSets[mapSetCount].acronym = acronym;
            mapSets[mapSetCount].authors = getDynLangEntry(acronym, "MAPSET", "AUTHORS");
            mapSets[mapSetCount].description = getDynLangEntry(acronym, "MAPSET", "DESCRIPTION");
            mapSets[mapSetCount].custom = getDynLangEntry(acronym, "MAPSET", "CUSTOM");
            mapSets[mapSetCount].maps = getDynLangEntry(acronym, "MAPSET", "MAPS");
            mapSets[mapSetCount].date = getDynLangEntry(acronym, "MAPSET", "DATE");
            if (CheckFont(strparam(s:acronym, s:"TN")))
            {
                mapSets[mapSetCount].thumbnail = StrParam(s:acronym, s:"TN");
            }
            else
            {
                mapSets[mapSetCount].thumbnail = "BLNKTN";
            }
            int j= 1;
            while(j < MAX_ACTORS)
            {
                mapSets[mapSetCount].actors[j] = getDynLangEntry(acronym, "ACTOR", strparam(i:j));
                mapSets[mapSetCount].alt_actors[j] = getDynLangEntry(acronym, "ACTORALT", strparam(i:j));
                j++;
            }
            j = 1;
            while(j < MAX_WEAPONS)
            {
                mapSets[mapSetCount].weapons[j] = getDynLangEntry(acronym, "WEAPON", strparam(i:j));
                j++;
            }
            j = 1;
            while(j < MAX_OBITUARIES)
            {
                mapSets[mapSetCount].obituaries[j] = getDynLangEntry(acronym, "OB", strparam(i:j));
                j++;
            }
            j = 1;
            while(j < MAX_TAGS)
            {
                mapSets[mapSetCount].tags[j] = getDynLangEntry(acronym, "TAG", strparam(i:j));
                j++;
            }
            j = 1;
            while(j < MAX_MESSAGES)
            {
                mapSets[mapSetCount].messages[j] = getDynLangEntry(acronym, "MSG", strparam(i:j));
                j++;
            }
            mapSetCount++;
        }
        i++;
    }
}
