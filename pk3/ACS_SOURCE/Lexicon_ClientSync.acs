int playerVotes[64][2] = {};
int winningSets[64][2] = {};
str VotesDisplay[64] = {};

int voteTimeLimit;

Script "Lexicon_SyncClientData" (void)
{
	int i = 0;
	//We don't need to do this in singleplayer
	if(SinglePlayer()) terminate;
		
	Delay(1);	
	
	//Generate all of the map info on the client. It's better off this way
	ACS_NamedExecuteWithResult("Lexicon_GenerateMaps_Client");
		
	//Sync current player votes
	for(i = 0; i < 64; i++)
		ACS_NamedExecuteWithResult("Lexicon_SendVoteToClient",i,playerVotes[i][0],playerVotes[i][1]);
}

Script "Lexicon_SendVoteToClient" (int player, int value1, int value2) clientside
{
	playerVotes[player][0] = value1;
	playerVotes[player][1] = value2;
}

script "Lexicon_PlayerConnect" (int type, int arg1, int arg2) EVENT
{
	if(!isVR())
		terminate;	
		
	//GAMEEVENT_PLAYERCONNECT
	if(type == 9)
	{
		ACS_NamedExecuteWithResult("Lexicon_SyncClientData");
	}
			
	while(PlayerIsSpectator(arg1))
	{
		ACS_NamedExecuteWithResult("Lexicon_VoteTextDisplay",VoteTimeLimit);

		Delay(1);
	}
}

Script "Lexicon_PlayerDisconnect" (int player) DISCONNECT
{
	if(!isVR())
		terminate;	
		
	removeVote(player);
	SetActivator(0,player);
	RemoveMessages(100,150);
	
	while(PlayerIsSpectator(player))
	{
		ACS_NamedExecuteWithResult("Lexicon_VoteTextDisplay",VoteTimeLimit);
		
		Delay(1);
	}
}